buildscript {
    ext.kotlinVersion = '1.1.3'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'signing'

archivesBaseName = baseArchiveName

sourceCompatibility = 1.8

repositories {
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

dependencies {
    compile "org.seleniumhq.selenium:selenium-java:3.4.0"
    compile group: 'org.aeonbits.owner', name: 'owner', version: '1.0.9'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"
    compile group: 'io.github.bonigarcia', name: 'webdrivermanager', version: '1.7.0'
    testCompile group: 'org.testng', name: 'testng', version: '6.11'
    testCompile 'me.tatarka.assertk:assertk:1.0-SNAPSHOT'
    testCompile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.6.v20170531'
    testCompile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.4.6.v20170531'
}

test{
    useTestNG()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}

jacoco {
    toolVersion = "0.7.2.201409121644"
}

jacocoTestReport {
    executionData = files("${project.buildDir}/jacoco/test.exec")
    reports {
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        xml.enabled true
        html.enabled true
        html.destination "${buildDir}/jacocoHtml"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}

modifyPom {
    project {
        name 'com.automation.remarks.kirk'
        packaging 'jar'
        description 'Kirk - pragmatic UI test automation'
        url 'https://github.com/SergeyPirogov/kirk'

        scm {
            url 'scm:git@github.com:SergeyPirogov/kirk.git'
            connection 'scm:git@github.com:SergeyPirogov/kirk.git'
            developerConnection 'scm:git@github.com:SergeyPirogov/kirk.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'SerhiiP'
                name 'Sergey Pirogov'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = false
    javadoc = true
}

nexus {
    sign = true
}